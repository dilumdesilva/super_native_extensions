group 'dev.irondash.engine_context'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'dev.irondash.engine_context'
    }

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }
}


// Check if pre-built binaries exist
def prebuiltLibsDir = file("src/main/jniLibs")
def hasPrebuiltLibs = prebuiltLibsDir.exists() && prebuiltLibsDir.listFiles()?.any { arch ->
    new File(arch, "libirondash_engine_context_native.so").exists()
}

if (hasPrebuiltLibs) {
    // Use pre-built binaries - no Rust compilation needed
    android {
        sourceSets {
            main {
                jniLibs.srcDirs = ['src/main/jniLibs']
            }
        }
    }
    println "Using pre-built native libraries for irondash_engine_context"
} else {
    // Fall back to building from source with Cargo
    apply from: "../cargokit/gradle/plugin.gradle"
    cargokit {
        manifestDir = "rust"
        libname = "irondash_engine_context_native"
    }
}
